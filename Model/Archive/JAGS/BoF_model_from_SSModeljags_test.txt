model
  {
    # Priors
    # Population scaling factor
    K ~ dlnorm(logK.a, logK.b)
    
    
    # priors for survey recruitment index
    for(t in 1:NY){
      r[t] ~ dlnorm(r.a, r.b)
    }
    
    # priors for natural mortality
    # prior for annual m
    for(t in 1:NY){
      m[t] ~ dlnorm(m.a, m.b)
    }
    
    #S=dissolution rate in days/365
    S~dunif(S.a,S.b)
    
    # priors for catchabilities
    # Use prior based on Bourdages' field work
    q ~ dbeta(q.a, q.b)
    
    # prior for process noise
    sigma ~ dunif(sigma.a, sigma.b)
    isigma2 <- pow(sigma, -2)
    
    # prior for clappers
    
    # clappers
    kappa.tau ~ dunif(kappa.tau.a, kappa.tau.b)
    ikappa.tau2 <- pow(kappa.tau,-2)
    
    # priors for observation errors
    
    for(t in 1:NY)
    {
      I.var[t]<-2*log(pow(I.cv[t],2)+1)
      I.precision[t] ~ dgamma(3,I.var[t])
      I.vsam[t]<-pow(I.precision[t],-0.5)
      IR.var[t]<-2*log(pow(IR.cv[t],2)+1)
      IR.precision[t] ~ dgamma(3,IR.var[t])
      IR.vsam[t]<-pow(IR.precision[t],-0.5)
    }
    
    # State equation
    Pmed[1] <- -pow(sigma,2)/2 
    P[1] ~ dlnorm(Pmed[1], isigma2)
    
    for(t in 2:NY){	
      Pmed[t] <- log(max(exp(-m[t]) * g[t-1] * (P[t-1] - C[t-1] / K) + exp(-m[t])*gR[t-1] * r[t-1], 0.001))
      P[t] ~ dlnorm(Pmed[t], isigma2)
    }
    
    #Natural mortality from survey clapper index, revised for popcorn model
    
    Cmed[1]<-log(m[1]*S*N[1])
    
    for(t in 2:NY){
      Cmed[t]<-log(m[t]*S*(S*N[t-1]+(2-S)*N[t])/2)
    }
    
    for(t in 1:NY){
      clappers[t]~dlnorm(Cmed[t],ikappa.tau2)
    }
    
    
    
    # Observation equations
    
    for(t in 1:NY){
      # Survey biomass
      Imed[t] <- log(q * K * P[t])
      I[t] ~ dlnorm(Imed[t], I.precision[t])
      
      # Survey recruitment
      IRmed[t] <- log(ratiolined[t]* q * K * r[t])
      IR[t] ~ dlnorm(IRmed[t], IR.precision[t])	
      
      
      # Predicted survey estimates	
      Ipred[t] <- q * B[t]
      IRpred[t] <-ratiolined[t]* q * R[t]
      Irep[t] ~ dlnorm(Imed[t], I.precision[t])#I(0,Ireplim)
      IRrep[t] ~ dlnorm(IRmed[t], IR.precision[t])#I(0,IRreplim)
      
    }
    
    
    # Population commercial and recruit biomass posteriors
    for(t in 1:NY){
      B[t] <- P[t] * K
      R[t] <- r[t] * K
    }
    
    # Exploitation and fishing mortality
    for(t in 1:(NY-1)){
      mu[t] <- C[t]/(B[t+1]+C[t])
      Fmort[t] <- -log(max(1 - mu[t], 0.0001))
    }
    
    # Diagnostics: raw and scaled residuals
    for(t in 1:NY){
      Iresid[t] <- log(I[t]) - Imed[t]
      Presid[t] <- log(P[t]) - Pmed[t]
      IRresid[t] <- log(IR[t]) - IRmed[t]
      
      sPresid[t] <- Presid[t] * 1/sigma
      sIresid[t] <- Iresid[t] * pow(I.precision[t],0.5)
      sIRresid[t] <- IRresid[t] * pow(IR.precision[t],0.5)
      
      
    }
  }